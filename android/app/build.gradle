plugins {
    id 'com.github.ben-manes.versions' version '0.46.0'
    id 'com.github.triplet.play' version '3.8.1' apply false
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keyProperties = new Properties()
def keyPropertiesFile = rootProject.file('../secrets/android/key.properties')
if (keyPropertiesFile.exists()) {
    keyProperties.load(new FileInputStream(keyPropertiesFile))
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
apply from: '../constants.gradle'

android {
    compileSdkVersion flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId constants.applicationId
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        manifestPlaceholders += [ applicationLabel:constants.appNameSnakeCase ]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keyProperties['releaseStoreFile'])
            storePassword keyProperties['releaseStoreAndKeyPassword']
            keyAlias keyProperties['releaseKeyAlias']
            keyPassword keyProperties['releaseStoreAndKeyPassword']
        }
        nonRelease {
            storeFile file(keyProperties['nonReleaseStoreFile'])
            storePassword keyProperties['nonReleaseStoreAndKeyPassword']
            keyAlias keyProperties['nonReleaseKeyAlias']
            keyPassword keyProperties['nonReleaseStoreAndKeyPassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.nonRelease
        }
        profile {
            applicationIdSuffix ".profile"
            versionNameSuffix "-profile"
            signingConfig signingConfigs.nonRelease
        }
        release {
            applicationIdSuffix ".release"
            versionNameSuffix "-release"
        }
    }

    flavorDimensions "env"
    productFlavors {
        dev {
            dimension "env"
            manifestPlaceholders = [ hostName:constants.devHostName ]
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            signingConfig signingConfigs.nonRelease
        }
        stag {
            dimension "env"
            manifestPlaceholders = [ hostName:constants.stagHostName ]
            applicationIdSuffix ".stag"
            versionNameSuffix "-stag"
            signingConfig signingConfigs.nonRelease
        }
        prod {
            dimension "env"
            manifestPlaceholders = [ hostName:constants.prodHostName ]
            applicationIdSuffix ".prod"
            versionNameSuffix "-prod"
            signingConfig signingConfigs.release
        }
    }
}

androidComponents {
    def all = selector().all()
    onVariants(all) { variant -> 
        if(variant.flavorName == "prod" && variant.buildType == "release") {
            variant.applicationId.set(android.defaultConfig.applicationId)
            variant.outputs.each { output ->
                if (output.versionName.get() == android.defaultConfig.versionName + "-prod-release") {
                    output.versionName.set(android.defaultConfig.versionName)
                }
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Source - https://github.com/flutter/flutter/tree/main/packages/integration_test#android-device-testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

task("printAllVariants") {
    doLast {
        println "applicationId,versionName,signingConfig.keyAlias"
        android.applicationVariants.all { variant ->
            println variant.applicationId + "," \
                + variant.versionName + "," \
                + variant.signingConfig.keyAlias
        }
    }
}

apply plugin: "com.github.triplet.play"
import com.github.triplet.gradle.androidpublisher.ResolutionStrategy

play {
    serviceAccountCredentials.set(file(keyProperties['gppServiceAccountKeyFile']))
    track.set("internal")
    resolutionStrategy.set(ResolutionStrategy.AUTO)
}

// TODO(hrishikesh-kadam): Remove this once the following issue is resolved.
// https://github.com/flutter/flutter/issues/88005
project.gradle.startParameter.excludedTaskNames.add(':integration_test:lintDebug')
